' Gambas module file

' Tool maintained by Terco
' PlantDesigner
' Software para diseño de plantas tipo de hormigon
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
' Gambas class file

' Credits
' --------
' Icon design por Trinh Ho ,Good Ware, freepik get them at https://www.flaticon.es
' Based on an example called MuchosCirculos by JGuardon y MrShell
' XML Load/Save by Tincho

Public ScreenDensity As Float = 1080 / 29.5  '' pixele by cm wich gives a 1:100 scale in my monitor

' Filtros del dibujo
Public DrawOriginals As Boolean = False
Public DrawMarcados As Boolean = True
Public DrawSoloColumnas As Boolean = False
Public DrawBounds As Boolean = False '' ¿Para que sirve esta variable?

Public ScaleLines As Float = 1              '' la uso en Inserts para evitar lineas gordas
Public FormatoCotas As String = "0.00"

Public ColorForSelected As Integer = &H70E000
Public ToolActive As Boolean

Public HookSize As Integer = 16                       '' for use with poi
Public Orthogonal As Boolean = False              '' idem Ortho F8 del CAD
Public OrthogonalForced As Boolean = False
Public OrthogonalIgnored As Boolean = False
Public SnapMode As Integer                      '' Bitwise: -1 Off ; 0 = Nearest; 1 = Endpoint; 2 = midpoint

Public clsJob As Object                             '' what I am doing now, thats either selecting or something else
Public clsJobPrevious As Object                     '' what was doing before
Public clsJobPreZoom As Object                      '' what was doing before Zooming or Panning
Public clsJobPreviousParam As Integer               '' a param to pass to clsJob.Start( param )

'Public gColor As New Integer[]

Public drwDrawingClass As Integer = 0 '' 0 = Paint, 1 = OpenGL
Public Const drwPaintClass As Integer = 2
Public Const drwOpenGLClass As Integer = 4

Public LoadingFinished As Boolean = False
Public FontList As New String[]                     '' esto deberia estar en otro lado
Public TextureList As New String[]                     '' esto deberia estar en otro lado
Public FileName As String                           '' current work filename

Public ShowInspector As Boolean = False ' TODO: Tincho, agregar a la configuracion y a un Toggle button
'---------------------------------------------------------------
' Point of Interest
Public Const poiEndPoint As Integer = 1
Public Const poiMidPoint As Integer = 2
Public Const poiQuadrant As Integer = 4
Public Const poiTangent As Integer = 8
Public Const poiCenter As Integer = 16
Public Const poiIntersection As Integer = 32
Public Const poiPerpendicular As Integer = 64
Public Const poiNearest As Integer = 128
Public Const poiBasePoint As Integer = 256

' Stipple lines
Public Const stiDashed As Integer = 1

' dim types
Public Const dimRotated As Integer = 0
Public Const dimAligned As Integer = 1
Public Const dimAngular As Integer = 2
Public Const dimDiameter As Integer = 3
Public Const dimRadius As Integer = 4
Public Const dimAngular3point As Integer = 5
Public Const dimOrdinate As Integer = 6
Public Const dimHorizontal As Integer = 7
Public Const dimVertical As Integer = 8

' Entity Flags
Public Const flgDWG_Changed As Integer = 1
Public Const flgDWG_UnChanged As Integer = 2
Public Const flgDWG_Deleted As Integer = 4
Public Const flgGCD_New As Integer = 8
'Public Const flg5 As Integer = 16
'Public Const flg6 As Integer = 32
'Public Const flg7 As Integer = 64
'Public Const flg8 As Integer = 128
'Public Const flg9 As Integer = 256

Public Drawings As New Drawing[]
Public DrawingReady As Boolean = False
Public CurrentDrawing As Drawing

'variables de ambiente
Public flgShowInspector As Boolean = False
Public flgSearchingEntity As Boolean = False  ' impide cuellos de botellla
Public flgSearchingPOI As Boolean
Public flgShowingLayers As Boolean
Public flgNewPosition As Boolean        ' seteada cuando hay un cambio en pan o zoom

' 2021 pongo esto por compatibilidad
Public flgWindowBackColor As Integer '= Color.Blac  '' Window backgrount color
Public flgWindowTextColor As Integer   '' Window helper text color
Public flgWindowCursorColor As Integer '' Window cursor color
Public flgWhiteAndBlack As Integer     '' El color blanco/negro siempre sera distinto al
Public flgOnScreenHelpColor As Integer = Color.LightGray    '' Tincho, esta es nueva

' Vars for entities edition
Public arrUndo As New Entity[]             '' An array to put an unmodified copy of an entity before modifying it and then restoring it to the previous version.

'' This transfer layers from imported file, one collection actually, to the model collection

Public Sub LayersTransfer()

  Dim tLay As New Collection
  Dim i As Integer
  Dim il As Variant
  Dim ml As Variant
  Dim prop As String
  Dim iLay As Collection
  Dim mLay As Collection
  Dim r As Integer

  If CurrentDrawing.cImport.Count > 0 Then
    If CurrentDrawing.cImport.Exist("Layers") Then
      iLay = CurrentDrawing.cImport["Layers"]
      If CurrentDrawing.cModel.Exist("Layers") Then
        mLay = CurrentDrawing.cModel["LAYERS"]

        For Each il In iLay
          r = 0
          For Each ml In mLay
            If ml["name"] = il["name"] Then
              r = -1
              Break
            Else
              Inc r
            Endif
          Next

          If r > -1 Then
            tLay.Add(Format(r, "0"), "id")
            tLay.Add("layer", "type")
            For Each prop In ["name", "show", "frozen", "locked", "color", "style", "width", "print"]
              tLay.Add(il[prop], prop)
            Next
            mLay.Add(tLay.copy(), Format(r, "0"))
            tLay.Clear
          Endif

        Next
      Else
        For Each il In iLay
          tLay.Add(Format(i, "0"), "id")
          tLay.Add("layer", "type")

          For Each prop In ["name", "show", "frozen", "locked", "color", "style", "width", "print"]
            tLay.Add(iLay[prop], prop)
          Next

          mLay.Add(tLay.copy(), Format(i, "0"))
          tLay.Clear
          Inc i
        Next
      Endif
    Endif
  Endif

End

Public Sub BlocksTransfer()

  Dim iEnt As Collection
  Dim mEnt As Collection
  Dim grp As Collection
  Dim e As Variant
  Dim mGrp As Collection

  If CurrentDrawing.cImport.Count > 0 Then
    If CurrentDrawing.cImport.Exist("Blocks") Then
      iEnt = CurrentDrawing.cImport["Blocks"]
      If CurrentDrawing.cModel.Exist("Blocks") Then
        mEnt = CurrentDrawing.cModel["Blocks"]

        For Each grp In iEnt
          If mEnt.Exist(iEnt.Key) = False Then
            mEnt.Add(grp.Copy(), iEnt.Key)
          Else
            mGrp = mEnt[iEnt.Key]
            For Each e In grp
              mGrp.Add(e, grp.Key)
            Next

          Endif

        Next
      Else

        CurrentDrawing.cModel.Add(iEnt.Copy(), "Blocks")

      Endif
    Endif
  Endif

End

Public Sub EntitiesTransfer2()

  Dim iEnt As Collection
  Dim mEnt As Collection
  Dim grp As Collection
  Dim e As Variant
  Dim mGrp As Collection

  If CurrentDrawing.cImport.Count > 0 Then
    If CurrentDrawing.cImport.Exist("Entities") Then
      iEnt = CurrentDrawing.cImport["Entities"]
      If CurrentDrawing.cModel.Exist("Entities") Then
        mEnt = CurrentDrawing.cModel["Entities"]

        For Each grp In iEnt
          If mEnt.Exist(iEnt.Key) = False Then
            mEnt.Add(grp.Copy(), iEnt.Key)
          Else
            mGrp = mEnt[iEnt.Key]
            For Each e In grp
              mGrp.Add(e, grp.Key)
            Next

          Endif

        Next

      Endif
    Endif
  Endif

End

Public Function GetLayerByHandle(sHandle As String) As Layer

  Dim Lay As Layer

  For Each Lay In gcd.CurrentDrawing.arrLayers

    If Lay.handle = sHandle Then Return Lay
  Next
  Return Null

End

Public Function GetLayer(LayerName As String) As Layer

  Dim Lay As Layer

  For Each Lay In gcd.CurrentDrawing.arrLayers

    If Lay.Name = LayerName Then Return Lay
  Next
  Return Null

End

Public Sub Main()

  Select Starter.flgWindowBackColor
    Case Color.Black
      'Case &1B2224
      Starter.flgWhiteAndBlack = Color.White

    Case Else
      Starter.flgWhiteAndBlack = Color.Black

  End Select
  Dim oLtype As New LType
  gcd.CurrentDrawing.arrLTYpes.Add(oLtype)
  gcd.CurrentDrawing.CurrLineType = oLtype

  Dim oNewLayer As New Layer
  gcd.CurrentDrawing.arrLayers.Add(oNewLayer)
  gcd.CurrentDrawing.CurrLayer = oNewLayer

  ' para compatibilidad
  flgWindowBackColor = Starter.flgWindowBackColor
  flgWindowTextColor = Starter.flgWindowTextColor
  flgWindowCursorColor = Starter.flgWindowCursorColor
  flgWhiteAndBlack = Starter.flgWhiteAndBlack
  flgOnScreenHelpColor = Starter.flgOnScreenHelpColor

  'If WindowBackColor = 0 Then WhiteAndBlack = Color.White Else WhiteAndBlack = Color.Black
  ' ' armo el array de colores
  ' gcolor = DecodeColor()
  '
  ' ' corrijo los colores que no se ven contra el fondo
  ' gcolor[0] = WhiteAndBlack
  ' gcolor[7] = WhiteAndBlack
  ' gcolor[255] = WhiteAndBlack

  ' this is what we are doing now
  clsJob = clsDefaultJob
  clsJob.start()
  clsJobPrevious = clsDefaultJob
  clsJobPreZoom = clsDefaultJob
  clsJobPreviousParam = 0

  'FileName = User.Home &/ "autosaveV5.xml"

  'FontList = glPlus.LoadFonts(Application.Path &/ "Fonts/LFF")
  FontList = glPlus.LoadFonts(Starter.sFonts)
  glPlus.SelectFont("romand")

  'texturelist = glPlus.LoadTextures(Application.Path &/ "Textures")
  texturelist = glPlus.LoadTextures(Starter.sTextures)

End

Public Sub ExportBlocks(colData As Collection) ', obxEntities As Entity[]) As Integer

End

Public Sub ImportBlocks(colData As Collection) ', obxEntities As Entity[]) As Integer

  Dim iTotalEntities As Integer, colent, colBlk As Collection, flxPoints As New Float[], P As Float[]
  Dim hBlock As String
  Dim cEntyList As New Collection
  Dim hEnty As Variant, iEnty As Integer
  Dim cEnty As Variant[]

  If Not colData.Exist("BLOCKS") Then Return
  For Each colBlk In colData["BLOCKS"]
    Dim newBlock As New Block
    hBlock = colBlk["block_entity"][3]
    newBlock.entities = New Entity[]
    newBlock.name = colBlk["name"]
    If colData["ENTITIES"].Exist(hBlock) Then
      newBlock.handle = colData["ENTITIES"][hBlock]["handle"][1]
      newBlock.layer = colData["ENTITIES"][hBlock]["layer"][3]
    Else If colData["NESTED"].Exist(hBlock) Then
      newBlock.handle = colData["NESTED"][hBlock]["handle"][1]
      newBlock.layer = colData["NESTED"][hBlock]["layer"][3]

    Endif
    If colBlk.Exist("entities") Then
      For Each cEnty In colBlk["entities"]
        hEnty = cEnty[3]
        If colData["NESTED"].Exist(hEnty) Then
          cEntyList.Add(colData["NESTED"][hEnty], hEnty)
        Else If colData["ENTITIES"].Exist(hEnty) Then
          cEntyList.Add(colData["ENTITIES"][hEnty], hEnty)

        End If
      Next
      ImpEnty5(cEntyList, newBlock.entities)
      cEntyList.Clear

    Else If colBlk.Exist("first_entity") Then
      For iEnty = CInt(colBlk["first_entity"][3]) To CInt(colBlk["last_entity"][3])
        ' pero no se si estan en Nested o en Entities
        hEnty = CStr(iEnty)
        If colData["NESTED"].Exist(hEnty) Then
          cEntyList.Add(colData["NESTED"][hEnty], hEnty)
        Else If colData["ENTITIES"].Exist(hEnty) Then
          cEntyList.Add(colData["ENTITIES"][hEnty], hEnty)

        End If
      Next
      ImpEnty5(cEntyList, newBlock.entities)
      cEntyList.Clear
    Else

      Debug "Error loading block", newBlock.name

    End If
    gcd.CurrentDrawing.arrBlocks.Add(newBlock)

  Next
  'Stop

End

'' Importa las cosas de manera descentralizada
Public Sub ImpEnty5(cEny As Collection, obx As Entity[])

  Dim e As Collection               ' Current Entity
  Dim entNueva As Entity

  For Each e In cEny                                              ' Para cada Coleccion de datos de vrx
    If e.Exist("entity") Then                                     ' es una entidad?

      If InStr("POINT ATTDEF ATTRIB LINE LWPOLYLINE CIRCLE ELLIPSE ARC TEXT MTEXT SPLINE SOLID LEADER INSERT DIMENSION_LINEAR DIMENSION_DIAMETER DIMENSION_RADIUS DIMENSION_ANG3PT DIMENSION_ALIGNED DIMENSION_ORDINATE LARGE_RADIAL_DIMENSION ARC_DIMENSION", UCase(e["entity"])) = 0 Then Continue
      entNueva = clsEntities.ImportJsonToEntity(e)

      If Not IsNull(entNueva) Then         ' si esta implementada, llenamos los datos

        If e.Exist("handle") Then

          obx.Add(entNueva)
          entNueva = Null                                           ' limpiamos
        Else                                                        ' no esta implementada
          Print "Entidad no implementada o con errores:", e["entity"]
        Endif
      End If
    Endif
  Next

End

'' Aplica los cambios en los Layers a las entidades
'' Si mode = 1, primero recontruye el layerID (para ser usado luego de abrir un archivo)

Public Function ApplyLayerProperties(Optional mode As Integer = 0)

  Dim e As Entity, cLayer As Collection
  'Dim LayerName As String

  Print "Aplicando propiedades del Layers"

  For Each e In gcd.CurrentDrawing.arrEntities

    If mode = 1 Then
      ' reconstruyo el ID del layer en la entidad, para ello busco en la lista cual coincide con su nombre
      For Each cLayer In gcd.CurrentDrawing.cModel["LAYERS"]
        If e.layer = cLayer["name"] Then
          e.layerID = cLayer["id"]
          Break
        Endif
      Next

    Endif

    ' nombre, el que pudo haber cambiado (exepto el "0")
    e.layer = gcd.CurrentDrawing.cModel["LAYERS"][e.layerID]["name"]

    ' visibiliad
    If gcd.CurrentDrawing.cModel["LAYERS"][e.layerID]["show"] Then e.visible = True Else e.visible = False

  Next

End

'' Aplica los cambios en los Layers a las entidades
'' Si mode = 1, primero recontruye el layerID (para ser usado luego de abrir un archivo)
Public Function ApplyLayerPropertieToEntity(e As Entity)

  Dim cLayer As Collection
  'Dim LayerName As String

  ' reconstruyo el ID del layer en la entidad, para ello busco en la lista cual coincide con su nombre
  For Each cLayer In gcd.CurrentDrawing.cModel["LAYERS"]
    If e.layer = cLayer["name"] Then
      e.layerID = cLayer["id"]
      Break
    Endif
  Next

  ' nombre, el que pudo haber cambiado (exepto el "0")
  e.layer = gcd.CurrentDrawing.cModel["LAYERS"][e.layerID]["name"]

  ' visibiliad
  If gcd.CurrentDrawing.cModel["LAYERS"][e.layerID]["show"] Then e.visible = True Else e.visible = False

End

Public Function GetLayerID(LayerName As String) As String

  Dim cLayer As Collection

  ' FIXME: reparar post layers
  Return "0"

  For Each cLayer In gcd.CurrentDrawing.cModel["LAYERS"]
    If cLayer["name"] = LayerName Then Return cLayer["id"]
  Next

End

' Public Function GetGBColor(CADcolor As Integer, Optional oLayer As Layer) As Integer
'
'   Dim layerID As String
'
'   If Not IsNull(oLayer) Then layerID = oLayer.Name
'
'   ' FIXME: reparar post layers
'   If Not gcd.CurrentDrawing.cModel["LAYERS"].Exist(layerID) Then Return Starter.flgWhiteAndBlack
'
'   ' color
'   If CADcolor = 256 Then          ' buscar color del layer
'
'     If TypeOf(gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]) = gb.Object Then
'       Return Starter.gColor[gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]["index"]]
'     Else
'       Return Starter.gColor[gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]]
'     Endif
'
'     'Return Starter.gColor[11]
'
'   Else If CADcolor = 257 Then     ' buscar color del objeto
'
'     If TypeOf(gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]) = gb.Object Then
'       Return Starter.gColor[gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]["index"]]
'     Else
'       Return Starter.gColor[gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]]
'     Endif
'     'Return Starter.gColor[gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]["index"]]    ' FIXME: cambiar esto
'     'Return Starter.gColor[12]
'
'   Else If CADcolor = 0 Then       ' buscar color del bloque
'     If TypeOf(gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]) = gb.Object Then
'       Return Starter.gColor[gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]["index"]]
'     Else
'       Return Starter.gColor[gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]]
'     Endif
'     'Return Starter.gColor[13]
'     'Return Starter.gColor[gcd.CurrentDrawing.cModel["LAYERS"][layerID]["color"]["index"]]   ' FIXME: cambiar esto
'
'   Else
'
'     Return Starter.gColor[CADcolor]
'
'   Endif
'
' End

Public Function GetGBColor(CADcolor As Integer, Optional pLayer As Layer) As Integer

  ' FIXME: reparar post layers
  'Return gcd.flgWhiteAndBlack
  If IsNull(pLayer) Then Return Starter.gColor[CADcolor]
  ' color
  If CADcolor = 256 Then          ' buscar color del layer

    Return Starter.gColor[player.Colour]

  Else If CADcolor = 257 Then     ' buscar color del objeto

    Return Starter.gColor[pLayer.Colour]

  Else If CADcolor = 0 Then
    ' buscar color del bloque
    Return Starter.gColor[pLayer.Colour]
  Else

    Return Starter.gColor[CADcolor]

  Endif

End

Public Function UUHandleUnique(d As Drawing) As String

  '5 Entity handle; text string Of up To 16 hexadecimal digits(fixed)
  Inc d.LastHandle
  Return Hex(d.LastHandle, 6)

End

'' Devuelve un handle disponible unico para la coleccion de datos.
Public Function GetNewHandle(c As Collection) As String

  Dim i As Integer, s As String

  i = c.Count
  Try i = CInt(c.Last)
  Do
    Inc i
    s = CStr(i) ' esto podria ser Hex
    If Not c.Exist(s) Then
      Return s
    Endif

  Loop

End

'' Updates the Graphics with the changes in the Model. Can be applied to certain entities only. Forces
'' a Regen if specified.
Public Function UpdateGraphics(Optional handles As String[] = [], DoRegen As Boolean = True)

  Dim cEntity As Collection, entNueva As Entity

  UpdateLayers
  JSONReadLTypes()

  ImportBlocks(gcd.CurrentDrawing.cModel)

  If handles.Count = 0 Then                   ' Update all entities

    gcd.ImpEnty5(gcd.CurrentDrawing.cModel["ENTITIES"], gcd.CurrentDrawing.arrEntities)

  Else                                        ' Update only entities with handles provided

    For Each sHandle As String In handles

      If gcd.CurrentDrawing.cModel["ENTITIES"].Exist(sHandle) Then  ' la actualizo

        entNueva = New Entity                                       ' entonces, creamos una nueva
        If clsEntities.ImportJsonToEntity(gcd.CurrentDrawing.cModel["ENTITIES"][sHandle], entNueva) Then         ' si esta implementada, llenamos los datos

          gcd.CurrentDrawing.arrEntities.Add(entNueva)
          entNueva = Null                                           ' limpiamos

        Endif
      Endif

    Next

  End If

  ReadEntities()

  If DoRegen Then Regen

End

Public Sub redraw()

  appmain.glarea1.Refresh

End

'' Regenera las listas de OpenGL
Public Sub Regen()

  clsEntities.glGenDrawList
  clsEntities.glGenDrawListSel
  redraw

End

'' Pasa los layers de cModel a los graficos
Public Sub UpdateLayers()

  Dim CMLayer As Collection
  Dim newlayer As Layer

  CurrentDrawing.arrLayers.Clear
  For Each CMLayer In CurrentDrawing.cModel["LAYERS"]
    newlayer = New Layer
    If TypeOf(CMLayer["color"]) = gb.Object Then
      newlayer.Colour = CMLayer["color"]["index"]
    Else
      newlayer.Colour = CMLayer["color"]
    Endif
    newlayer.Name = CMLayer["name"]
    newlayer.handle = CMLayer["handle"][1]
    newlayer.id = newlayer.handle
    newlayer.Visible = True
    CurrentDrawing.arrLayers.Add(newlayer)
  Next
  CurrentDrawing.CurrLayer = CurrentDrawing.arrLayers.First

End

Public Sub UpdateModel()

  Dim ent As Entity
  Dim JSONExporting As Boolean
  Dim jscNewEntity As JSONCollection
  'Dim sNewHandle As String
  Dim iHandle As Integer

  For Each ent In CurrentDrawing.arrEntities

    JSONExporting = False               ' Supongo que la clase todvia no esta lista

    ' Esto puede fallar en caso q no este lista, por eso el Try
    Try JSONExporting = ent.ClassHandler.JSON_Export

    If JSONExporting Then               ' La clase esta lista para crear/editar JSON

      ' Verifico si la entidad ya existe en cModel, o fue creada por el usuario graficamente
      If CurrentDrawing.cModel["ENTITIES"].Exist(ent.Handle) Then

        ' Ya existe

        ent.ClassHandler.ExportJsonCol(ent, CurrentDrawing.cModel["ENTITIES"][ent.Handle], False)

      Else
        ' No existe, debemos crear la entrada
        jscNewEntity = New JSONCollection
        'sNewHandle = GetNewHandle(CurrentDrawing.cModel["ENTITIES"])
        iHandle = CurrentDrawing.cModel["AuxHeader"]["HANDSEED"]
        CurrentDrawing.cModel["ENTITIES"].Add(jscNewEntity, CStr(iHandle + 1))
        ent.Handle = CStr(iHandle + 1) 'sNewHandle

        ' ahora metemos los datos communes
        ' clsEntities.JSON_Export(ent, CurrentDrawing.cModel["ENTITIES"][ent.Handle])

        ' y los datos particulares
        ent.ClassHandler.ExportJsonCol(ent, CurrentDrawing.cModel["ENTITIES"][ent.Handle], True)

        CurrentDrawing.cModel["AuxHeader"]["HANDSEED"] = iHandle + 1

      End If
    End If

  Next

End

Public Function Xreal(ScreenX As Float) As Float

  Return Metros((screenx - appmain.glarea1.w / 2 - gcd.CurrentDrawing.PanX))

End

Public Function Yreal(ScreenY As Float) As Float

  Return Metros((-ScreenY + appmain.glarea1.h / 2 - gcd.CurrentDrawing.PanY))

End

Public Function Pixels(distancia As Float) As Float  ' converts meters to pixels
  ' transforma Metros en Pixeles sin tener en cuenta el Pan

  Return distancia * gcd.CurrentDrawing.ScaleZoom

End

Public Function Metros(pixeles As Integer) As Float  ' converts pixels to meters

  Return pixeles / gcd.CurrentDrawing.ScaleZoom

End

Public Function Near(xyzReal As Float) As Float
  ' return the nearest point to the grid
  ' this is a world to world points (not pixels)

  ' Example:
  ' if                    GridSpacing = 0.2
  ' we pass               xyzReal = 1.35
  ' function will give    NearReal = 1.40

  Dim n As Integer, r As Float

  If Not appmain.Gridactive Then Return xyzReal

  r = xyzReal / appmain.GridSpacing
  n = Int(r)
  r = r - Int(r)

  If r > 0.5 Then n += 1

  Return n * appmain.GridSpacing

End

'' Reads LTypes collection and puts data in arrLTypes
Public Sub JSONReadLTypes()

  Dim hlty As LType
  Dim t, i As Integer
  Dim fTrameLength As Float
  Dim sNextKey As String

  ' primero eliminamos lo q haya
  CurrentDrawing.arrLTYpes.Clear
  For Each c As JsonCollection In CurrentDrawing.cModel["LTYPES"]
    hlty = New LType
    hlty.Name = c["name"]
    hlty.Description = c["description"]
    hlty.handle = c["handle"][1]
    ' hlty.nTrames = CInt(c["73"])
    ' If hlty.nTrames > 0 Then hlty.Length = c["40"]
    ' i = 0
    ' For t = 1 To hlty.nTrames
    '   Do
    '     If t > 1 Then
    '       sNextKey = "49_" & CStr(i)
    '     Else
    '       sNextKey = "49"
    '     Endif
    '     If c.Exist(sNextKey) Then
    '       Inc i
    '       hlty.TrameLength.Add(Abs(CFloat(c[sNextKey])) * 2.5) ' TODO: verificar este valor arbitrario
    '       If hlty.TrameLength.Last = 0 Then hlty.TrameLength.Last = 1
    '
    '       Break
    '     Endif
    '     Inc i
    '     If i = 1000 Then Return
    '   Loop

    'Next

    CurrentDrawing.arrLTYpes.Add(hlty)

  Next
  CurrentDrawing.CurrLineType = CurrentDrawing.arrLTYpes.First

End

Public Function FindLtIndex(sName As String) As Integer

  Dim i As Integer

  For i = 0 To gcd.CurrentDrawing.arrLTYpes.Max

    If LCase(gcd.CurrentDrawing.arrLTYpes[i].Name) = LCase(sname) Then Return i

  Next

  Return -1

End

'' Reads entities and fill arrLayers
Public Sub ReadEntities()

  Dim hLay As Layer
  Dim e As Entity

  ' primero eliminamos lo q haya
  For Each hLay In CurrentDrawing.arrLayers
    hLay.Entities.Clear
  Next

  For Each e In CurrentDrawing.arrEntities
    'e.pLayer = CurrDrawing.arrLayers[gcd.FindLaIndex(e.pLayer)]
    e.pLayer.Entities.Add(e)
  Next

  currentDrawing.arrVisibleEntities.Resize(CurrentDrawing.arrEntities.Count)

End
