' Gambas class file

' GauchoCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
Create Static
' Fast

Public Const Gender As String = "Solid"
Public Const PointsToDraw As Integer = 0             ' minimal point requered for drawing something usefull
Public Const HelperTrabajo As String = ("Solid")
Public Const ParamType As String = "PPPP"                             ' that is Point, Point, Float; could be Color Text, etc
Public Const ParamHelper As String = "First Point;Next Point;Next Point;Last Point"      ' a little text that is shown at the prompt waiting for user input during build
Public Const ParamDefault As String = ""
Public Const GAMBASCADENTITY As String = "EntityBase"
Public Const USEWITH As String = "clsEntityBuilder"
Public Const DrawingOrder As Integer = 0        ' 1 = draws first
Public Const OrthogonalIgnored As Boolean = False
Public Const OrthogonalForced As Boolean = False
Public Const StretchAble As Boolean = True           ' Si lo puedo deformar
Public Selected As Boolean
Public ForDeletion As Boolean = True        ' this must be falsed in Finish
Public DrawAble As Boolean = False          ' True si durante la construccion ya tiene suficientes elementos para poder hacer una representacion digna
Public Visible As Boolean = True            ' si esta entidad puede ser mostrada independientmente
Public Finished As Boolean = False          ' colocar en true cuando se llegue a completar
Public StepsDone As Integer                 ' esto lo cambia cls ElementBuilder durante la construccion

' JSON and DXF Read/Export
Public Const JSON_Reading As Boolean = True
Public Const JSON_Export As Boolean = False

Public Const DXF_Reading As Boolean = True
Public Const DXF_Export As Boolean = True


Public Sub Translate(e As Entity, dX As Float, dY As Float, Optional OnlySelected As Boolean = False)

  Dim i As Integer

  If OnlySelected Then

    For i = 0 To e.Psel.Max
      If e.Psel[i] Then

        e.P[i * 2] += dx
        e.P[i * 2 + 1] += dy

      Endif
    Next

  Else

    puntos.Translate(e.p, dx, dy)
  Endif

End

Public Sub Rotate(e As Entity, radians As Float)

  puntos.Rotate(e.P, radians)

End

Public Sub Scale(e As Entity, sX As Float, sY As Float)

  Dim i As Integer

  For i = 0 To e.p.max Step 2

    e.P[i] = e.P[i] * sX
    e.P[i + 1] = e.P[i + 1] * sY

  Next

End

Public Sub Draw(oE As Entity)

  If Not oE.Visible Then Return

  oE.ColorToPaint = gcd.GetGBColor(oE.colour, oE.Layerid)

  If Not oe.Finished Then
    If Me.StepsDone < 1 Then

      oE.P[2] = oE.P[0]
      oE.P[3] = oE.P[1]

      glPlus.Lines(oE.p, oE.ColorToPaint, oE.LineWidth)

    End If

    If Me.StepsDone < 2 Then

      oE.P[4] = oE.P[2]
      oE.P[5] = oE.P[3]

    End If

    If Me.StepsDone < 3 Then

      oE.P[6] = oE.P[4]
      oE.P[7] = oE.P[5]

    Endif

  End If

  If oe.P.Count < 2 Then Return
  GLplus.PolygonFilled(oE.p, oE.ColorToPaint, oE.ColorToPaint, oE.LineWidth, oe.LineType)

End

'' A fat version of the entity
Public Sub DrawSelected(oE As Entity)

  GLplus.Polygon(oE.p, Starter.flgWindowBackColor, oE.LineWidth, gcd.stiDashed)
  GLplus.Polygon(oE.p, gcd.ColorForSelected, oE.LineWidth, oE.LineType)

End

'' A fat version of the line
Public Sub DrawRemark(oE As Entity)

  '  GLplus.Polygon(oE.p, Starter.flgWindowBackColor, oE.LineWidth, gcd.stiDashed)
  '  GLplus.Polygon(oE.p, oE.ColorToPaint, oE.LineWidth + 2, oE.LineType)
  If oe.Selected Then
    GLplus.Polygon(oE.P, Starter.flgWindowBackColor, oE.LineWidth + 2, gcd.stiDashed)
    GLplus.Polygon(oE.P, gcd.ColorForSelected, oE.LineWidth + 2, oE.LineType)
  Else
    GLplus.Polygon(oE.P, Starter.flgWindowBackColor, oE.LineWidth, gcd.stiDashed)
    GLplus.Polygon(oE.P, oE.ColorToPaint, oE.LineWidth + 2, oE.LineType)
  Endif

End

Public Function Draw2(oE As Entity)

  If Not oE.Visible Then Return

  Dim i As Integer

  If oE.Selected And Not gcd.ToolActive Then
    paint.brush = Paint.Color(gcd.ColorForSelected)
  Else
    paint.brush = Paint.Color(oE.ColorToPaint)
  Endif

  Paint.Polygon(oE.p)

  paint.Fill

End

'' Creo los puntos de interes
'' Build point of interest

Public Function BuildPOI(oE As Entity, Optional arrIndex As Integer = -1) As Integer

  cadLWPolyline.BuildPOI(oe)

End
' This function will return False if the element couldn't be created for some reason: bad or incomple user input
' True means all is good, False will instruct the clsEntityBuilder to delete it

Public Sub Finish(oE As Entity, Optional ParamsDone As Integer = -1) As Boolean

  'Print "Finish en EntityBase"
  'CreatePolygon(oe)
  BuildPOI(oe)
  oE.Polygon = oE.P

  ' busco el tipo de linea, si hace falta
  If oE.LineTypeName = "Dashed" Then oE.LineType = 1
  ' tareas finales de armado del objeto

  oE.psel.resize(oE.p.count / 2)
  oE.ForDeletion = False
  oE.DrawAble = True
  oE.Selected = False
  oE.Finished = True

  If Not True Then

    oE.ForDeletion = True
    oE.DrawAble = False

  End If

End

' Importo los datos que puedo procesar de una coleccion y genero un Entity para ser agregado a un array
Public Function ImportJsonCol(c As Collection, e As Entity) As Boolean

  e.ClassHandler = Me
  e.P.Add(c["corner1"][0])
  e.P.Add(c["corner1"][1])
  e.P.Add(c["corner2"][0])
  e.P.Add(c["corner2"][1])
  e.P.Add(c["corner3"][0])
  e.P.Add(c["corner3"][1])
  e.P.Add(c["corner4"][0])
  e.P.Add(c["corner4"][1])

  Return True

End

' "entity": "SOLID",
' "index": 14873,
' "type": 31,
' "handle": [0, 1412074],
' "size": 123,
' "bitsize": 914,
' "eed": [
'   {
'     "size": 25,
'     "handle": [5, 18],
'     "code": 10,
'     "value": [0.0, 0.0, 0.0]
'   }
' ],
' "ownerhandle": [12, 3, 112165, 1299909],
' "_subclass": "AcDbEntity",
' "layer": [5, 2, 16840, 16840],
' "preview_exists": 0,
' "entmode": 0,
' "nolinks": 1,
' "color": 5,
' "ltype_scale": 1.0,
' "ltype_flags": 0,
' "plotstyle_flags": 0,
' "invisible": 0,
' "linewt": 29,
' "_subclass": "AcDbTrace",
' "thickness": 0.000000,
' "elevation": 0.00000000000068,
' "corner1": [3031.97616104186818, 1631.40569016283212],
' "corner2": [3031.97616104186818, 1631.80569016283198],
' "corner3": [3031.97696102900818, 1631.40569016283212],
' "corner4": [3031.97696102900818, 1631.40569016283212],
' "extrusion": [0.0, 0.0, 1.0]


Public Sub ExportDXF(e As Entity, hFile As File)

  Dim i As Integer

  Print #hFIle, "  0"                       ' Entity type
  Print #hFIle, "SOLID"
  clsEntities.DXFExportCommon(e, hFile)
  Print #hFIle, "100"                     ' Subclass marker
  Print #hFIle, "AcDbTrace"
  Print #hFIle, " 90"                      ' cantidad de puntos
  Print #hFIle, e.p.Count
  Print #hFIle, " 70"                      ' 0 = abierta; 1 = cerrada
  Print #hFIle, "0"
  Print #hFIle, " 43"                      ' constant width  0 = constant
  Print #hFIle, "0"
  For i = 0 To e.p.Max Step 2
    Print #hFIle, " 1" & Trim$(Str$(Int(i / 2)))                      ' Xi
    Print #hFIle, CStr(e.p[i])
    Print #hFIle, " 2" & Trim$(Str$(Int(i / 2)))
    Print #hFIle, CStr(CStr(e.p[i + 1]))
    Print #hFIle, " 3" & Trim$(Str$(Int(i / 2)))
    Print #hFIle, "0"
  Next

End

Public Function ImportDXF(e As Entity, sClaves As String[], sValues As String[]) As Boolean

  Dim i As Integer

  For i = 0 To sClaves.Max

    If sClaves[i] = "10" Then e.p.Add(CFloat(sValues[i]))
    If sClaves[i] = "20" Then e.p.Add(CFloat(sValues[i]))
    If sClaves[i] = "11" Then e.p.Add(CFloat(sValues[i]))
    If sClaves[i] = "21" Then e.p.Add(CFloat(sValues[i]))
    If sClaves[i] = "12" Then e.p.Add(CFloat(sValues[i]))
    If sClaves[i] = "22" Then e.p.Add(CFloat(sValues[i]))
    If sClaves[i] = "13" Then e.p.Add(CFloat(sValues[i]))
    If sClaves[i] = "23" Then e.p.Add(CFloat(sValues[i]))

  Next

End