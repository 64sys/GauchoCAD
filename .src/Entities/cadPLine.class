' Gambas class file

' Represents a poli line
' Element maintained by Terco
' V2
Inherits EntityBase
Public Const GENDER As String = ("PLine")          
Public Const PointsToDraw As Integer = 0             ' minimal point requered for drawing something usefull
Public Const HelperTrabajo As String = ("Poliline")
Public Const ParamType As String = "P+"                             ' that is Point, Point, Float; could be Color Text, etc
Public Const ParamHelper As String = ("Next Point")      ' a little text that is shown at the prompt waiting for user input during build
Public Const ParamDefault As String = "Close/Undo"



Fast Public Sub Draw()
    If Not Me.DrawAble Or Not Me.Visible Then Return
    If Me.Selected And Not modCAD.ToolActive Then Me.ColorToPaint = modCAD.ColorForSelected Else Me.ColorToPaint = Me.gbColor

    
    Dim i As Integer
    
    If modCAD.drwDrawingClass = modCAD.drwPaintClass Then
    
        paint.LineWidth = Me.LineWidth / appmain.hFcad.ScaleZoom / modCAD.ScaleLines
        If Me.flxDash Then paint.Dash = Me.flxDash
        If Me.Selected And Not modCAD.ToolActive Then
            paint.brush = Paint.Color(modCAD.ColorForSelected)
        Else
            paint.brush = Paint.Color(Me.ColorToPaint)
        Endif
        
        paint.moveto(Me.p[0], Me.p[1])
        For i = 2 To Me.P.Max Step 2
            paint.lineto(Me.p[i], Me.p[i + 1])
            
        Next
       
        paint.Stroke
    End If
    
    
    If modCAD.drwDrawingClass = modCAD.drwOpenGLClass Then
            
            GLplus.PolyLines(Me.p, Me.ColorToPaint, Me.LineWidth)
            

    Endif

    
 
End

'' Creo los puntos de interes
'' Build point of interest

Public Function BuildPOI() As Integer
    
    
    Dim I, poiCount As Integer
    
    Me.MyPoi.Clear
    ' P0
    Me.MyPoi.Insert([Me.p[0], Me.p[1]])
    
    For i = 0 To Me.P.Max - 2 Step 2
    
        ' MID
        Me.MyPoi.Insert([(Me.p[i + 2] + Me.p[i]) / 2, (Me.p[i + 3] + Me.p[i + 1]) / 2])
        
        ' Pi+1
        Me.MyPoi.Insert([Me.p[i + 2], Me.p[i + 3]])
        
        ' FIXME: this limit is for idiot plines
        If i >= 100 Then Break
        
    Next
    
    Return Me.MyPoi.Count / 2
    
End


