' Gambas class file

' GauchoCAD
' A simple CAD made in Gambas
'
' Copyright (C) Ing Martin P Cristia
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
Create Static
Fast
Inherits EntityBase
Public Const GENDER As String = "Line"
Public Const DrawingOrder As Integer = 1        ' 1 = draws first
Public Const HelperTrabajo As String = ("a line")
Public Const ParamType As String = "PP"                             ' that is Point, Point; could be Color Text, etc
Public Const ParamHelper As String = "Start point;End point"

Public Sub Draw(oE As Entity)

    If Not oE.Visible Then Return

    If oE.Selected And Not modCAD.ToolActive Then oE.ColorToPaint = modCAD.ColorForSelected Else oE.ColorToPaint = modCAD.GetGBColor(oE.colour, oE.Layerid)

    GLplus.Lines(oE.p, oE.ColorToPaint, oE.LineWidth, oE.LineType)

End

Public Function Draw2(oE As Entity)

    If Not oE.Visible Then Return

    If oE.Selected And Not modCAD.ToolActive Then oE.ColorToPaint = modCAD.ColorForSelected Else oE.ColorToPaint = modCAD.GetGBColor(oE.colour, oE.Layerid)
    paint.LineWidth = oE.LineWidth / appmain.hFcad.ScaleZoom / modCAD.ScaleLines
    If oE.flxDash Then paint.Dash = oE.flxDash
    paint.brush = Paint.Color(oE.ColorToPaint)
    paint.MoveTo(oE.p[0], oE.p[1])
    paint.LineTo(oE.p[2], oE.p[3])
    paint.Stroke

End

'' Creo los puntos de interes
'' Build point of interest

Public Function BuildPOI(oE As Entity, Optional arrIndex As Integer = -1) As Integer

    If arrIndex < 0 Then arrIndex = appmain.hFCAD.arrEntities.Max       ' si este valor no es provisto , estos POI son de la ultima entidad

    ' points: end-mid-end
    appmain.hFCAD.poiPoints.insert([oE.p[0], oE.p[1]])
    appmain.hFCAD.poiType.Add(gcd.poiEndPoint)
    APPMain.hFCAD.poiEntities.Add(arrIndex)

    appmain.hFCAD.poiPoints.insert([(oE.P[0] + oE.P[2]) / 2, (oE.P[1] + oE.P[3]) / 2])   ' mid
    appmain.hFCAD.poiType.Add(gcd.poiMidPoint)
    APPMain.hFCAD.poiEntities.Add(arrIndex)

    appmain.hFCAD.poiPoints.insert([oE.p[2], oE.p[3]])
    appmain.hFCAD.poiType.Add(gcd.poiEndPoint)
    APPMain.hFCAD.poiEntities.Add(arrIndex)

    ' line
    appmain.hFCAD.poiLines.insert([oE.p[0], oE.p[1], oE.p[2], oE.p[3]])
    APPMain.hFCAD.poiLinesEntities.Add(arrIndex)

End

'' Se fija si la posicion indicada esta aproximadamente sobre la entidad, de acuerdo a la tolerancia
'' mode es para establecer si se busca especificamente algun punto y devolver su posicion y tipo.
'' Mode:

'' Devuelve [x,y] punto de interes
Public Function AboveMe(oE As Entity, Xreal As Float, Yreal As Float, tolerance As Float, mode As Integer, fData As Float[]) As Boolean
    ' si una de las dos lineas de una cruz con centro en X,Y y largo Tolerance cruza la linea, estamos arriba
    'Dim fData As New Float[]

    Dim fDist1, fDist2, fDist3, fDist4 As Float
    'TODO: para debug, agrego esto
    mode = 1 + 2 ' endpoint+midpoint

    fData.Clear
    If puntos.doIntersect(xreal - tolerance / 2, Yreal, Xreal + tolerance / 2, Yreal, oE.P[0], oE.P[1], oE.P[2], oE.P[3]) Or puntos.doIntersect(xreal, Yreal - tolerance / 2, Xreal, Yreal + tolerance / 2, oE.P[0], oE.P[1], oE.P[2], oE.P[3]) Then ' its above me

        ' busco el punto exacto en la linea donde se produce la interseccion
        fData.Insert(puntos.lineLineIntersection([xreal - tolerance / 2, Yreal], [Xreal + tolerance / 2, Yreal], [oE.P[0], oE.P[1]], [oE.P[2], oE.P[3]]))
        ' veo que punto esta mas cerca
        fDist1 = puntos.distancia(oE.P[0], oE.P[1], fData[0], fData[1])   ' end 1
        fDist2 = puntos.distancia((oE.P[0] + oE.P[2]) / 2, (oE.P[1] + oE.P[3]) / 2, fData[0], fData[1])   ' mid
        fDist3 = puntos.distancia(oE.P[2], oE.P[3], fData[0], fData[1])   ' end 2
        If fdist1 > fdist2 Then
            If fDist2 > fDist3 Then         ' End 2
                fData[0] = oE.P[2]
                fData[1] = oE.P[3]
                fData.Add(1)                 ' 1 = EndPoint
            Else
                fData[0] = (oE.P[0] + oE.P[2]) / 2
                fData[1] = (oE.P[1] + oE.P[3]) / 2
                fData.Add(2)                 ' 2 = MidPoint
            Endif
        Else
            fData[0] = oE.P[0]
            fData[1] = oE.P[1]
            fData.Add(1)                     ' 1 = EndPoint

        Endif

        Return True

    Endif

    Return False

End
